plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'com.mikepenz.aboutlibraries.plugin'
}

android {
    namespace = 'uk.trigpointing.android'
    compileSdk = 36

    defaultConfig {
        applicationId "uk.trigpointing.android"
        minSdk = 27
        targetSdk = 36
        versionCode 27
        versionName "2.0.16"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        def buildTime = new Date().format("yyyy-MM-dd HH:mm:ss")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    // Show detailed deprecation warnings
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
    
    lint {
        disable 'MissingTranslation'
        baseline = file("lint-baseline.xml")
    }
    
    // Make source sets explicit so IDEs reliably detect classpath
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }
        androidTest {
            java.srcDirs = ['src/androidTest/java']
        }
        test {
            java.srcDirs = ['src/test/java']
        }
    }
    
    // Compose setup
    buildFeatures {
        compose true
        buildConfig true
    }
    // Compose compiler plugin is added via Kotlin 2.0+; no manual extensionVersion needed
    packagingOptions {
        resources {
            excludes += [
                    '/META-INF/{AL2.0,LGPL2.1}',
                    'META-INF/LICENSE*'
            ]
        }
        // Help with 16KB page size compatibility
        jniLibs {
            useLegacyPackaging = false
        }
    }
}

// Ensure JDK 17 toolchain is used (required by AGP 8.x)
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    // Compose BOM
    implementation platform('androidx.compose:compose-bom:2025.08.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.2'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Images
    implementation 'io.coil-kt:coil-compose:2.7.0'
    implementation 'io.coil-kt:coil:2.7.0'
    implementation 'androidx.exifinterface:exifinterface:1.4.1'

    // WorkManager for background uploads
    implementation 'androidx.work:work-runtime-ktx:2.10.3'

    // Fragments and lifecycle
    implementation 'androidx.fragment:fragment-ktx:1.8.9'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.2'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.browser:browser:1.9.0'
    
    // HTTP client
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'
    
    // JSON parsing
    implementation 'com.google.code.gson:gson:2.11.0'
    
    // Logging - replacing slf4j-android-1.6.1-RC1.jar
    implementation 'org.slf4j:slf4j-android:1.7.36'
    
    // ACRA for crash reporting - replacing acra-4.2.3.jar
    
    // Maps - we'll need to replace the old maps.jar with Google Maps
    implementation 'com.google.android.gms:play-services-maps:19.2.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    

    implementation 'com.google.firebase:firebase-analytics:23.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:20.0.0'
    implementation 'com.google.firebase:firebase-perf:22.0.0'

    // Room for database
    implementation "androidx.room:room-runtime:2.7.2"
    annotationProcessor "androidx.room:room-compiler:2.7.2"
    implementation "androidx.webkit:webkit:1.14.0"
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.19.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    implementation 'org.apache.commons:commons-compress:1.28.0'

    // Testing - Unit Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.mockito:mockito-inline:4.11.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    
    // Testing - Android Integration Tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    
    // Testing - UI Tests (Espresso)
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    
    // Testing - Database
    androidTestImplementation 'androidx.room:room-testing:2.4.3'
    implementation "com.mikepenz:aboutlibraries:12.2.4"
}

